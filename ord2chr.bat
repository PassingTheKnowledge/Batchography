@echo off

	setlocal enabledelayedexpansion

:main

	for /l %%a in (1, 1, 20) do (
		call :get-plate-number
		echo !result!
	)
	goto :eof


:get-plate-number
	call :gen-rand-str 3 26 65
	set P=%result%

	call :gen-rand-str 4 10 48
	set N=%result%

	set result=%P%-%N%

	goto :eof

:gen-rand-str <1=length> <2=mod> <3=base> => %result%
	set str=
	for /l %%a in (1, 1, %1) do (
		set /A N="(!RANDOM! %% %2) + %3"
		call :ord2chr !N!
		set str=!str!!result!
	)
	set result=%str%

	goto :eof

:ord2chr <1=ordinal> => %result%
	:: Generated by gen-tables.py
	for %%a in ("65=A" "66=B" "67=C" "68=D" "69=E" "70=F" "71=G" "72=H" "73=I" 
		        "74=J" "75=K" "76=L" "77=M" "78=N" "79=O" "80=P" "81=Q" "82=R" 
		        "83=S" "84=T" "85=U" "86=V" "87=W" "88=X" "89=Y" "90=Z" "97=a" 
		        "98=b" "99=c" "100=d" "101=e" "102=f" "103=g" "104=h" "105=i" 
		        "106=j" "107=k" "108=l" "109=m" "110=n" "111=o" "112=p" "113=q" 
		        "114=r" "115=s" "116=t" "117=u" "118=v" "119=w" "120=x" "121=y" 
		        "122=z" "48=0" "49=1" "50=2" "51=3" "52=4" "53=5" "54=6" "55=7" 
		        "56=8" "57=9") do (
		for /f "usebackq tokens=1-2 delims==" %%b in ('%%~a') do (
			if "%1"=="%%b" (
				set result=%%c
				goto :eof
			)
		)
	)